; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
WizardImageFile=compiler:WizClassicImage.bmp
AppName=PyMOL-open-source
AppVersion=3.0.0
AppCopyright=Hannah Kullik, Martin Urban, Schrodinger LLC
AppId={{192F52C3-D86D-4735-9929-C7DF593CB534}
DefaultDirName={localappdata}\PyMOL-open-source
AppPublisher=Hannah Kullik
VersionInfoProductName=PyMOL-open-source
MinVersion=10.0.19045
OutputDir=out
OutputBaseFilename=setup
; VersionInfoCopyright=GNU GPL-3.0
DisableDirPage=True
DisableProgramGroupPage=True
ArchitecturesInstallIn64BitMode=x64
WizardStyle=modern
DisableReadyPage=True
DisableFinishedPage=True
UninstallDisplayName=PyMOL-open-source
UninstallDisplayIcon={app}\assets\logo.ico
LicenseFile=LICENSE.txt
; This is necessary if the setup will exceed 2 GB
DiskSpanning=yes
DiskSliceSize=2100000000

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Dirs]
Name: "{localappdata}\PyMOL-open-source"
Name: "{localappdata}\PyMOL-open-source\bin"
Name: "{localappdata}\PyMOL-open-source\tmp"

[Files]
Source: "src\PostInstallationRunner.exe"; DestDir: "{localappdata}\IBCI\helpers"; Flags: ignoreversion
Source: "src\offline_resources\windows_package.zip"; DestDir: "{localappdata}\IBCI\temp"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "src\offline_resources\alma-colabfold-9-rootfs.tar"; DestDir: "{localappdata}\IBCI\temp"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "src\offline_resources\ChimeraX-1.8.exe"; DestDir: "{localappdata}\IBCI\temp"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "src\install.bat"; DestDir: "{localappdata}\IBCI\temp"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "src\uninstall.bat"; DestDir: "{localappdata}\IBCI\helpers"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "src\assets\logo.ico"; DestDir: "{localappdata}\IBCI\PyDD\assets"; Flags: ignoreversion recursesubdirs createallsubdirs;

[UninstallDelete]
Type: filesandordirs; Name: "{localappdata}\PyMOL-open-source"

[Code]
var 
  tmpResultCode: Integer; 
  PostInstallFailed: Boolean;

procedure InitializeWizard;
begin
  // Initialize the global variable
  PostInstallFailed := False;
end;

function DoPostInstallTasks: Boolean;
// Return True if successful, False if there was an error
begin
  try
    MsgBox('A post installation task needs to be run. This will take around 5 minutes to complete. Press OK to start.', mbInformation, MB_OK);
    if Exec('C:\ProgramData\IBCI\helpers\PostInstallationRunner.exe', '', '', SW_HIDE, ewWaitUntilTerminated, tmpResultCode) then // Runs PostInstallationRunner!!
    begin
      // Check the result code of the executed program
      if tmpResultCode <> 0 then
      begin
        // Return False to indicate failure
        Result := False;
      end
      else
      begin
        // Task succeeded
        Result := True;
      end;
    end
    else
    begin
      // Failed to execute the external program
      Result := False;
    end;
  except
    begin
      // Return False to indicate failure
      Result := False;
    end;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    // Call a custom function to perform post-installation tasks
    if not DoPostInstallTasks then
    begin
      MsgBox('The installation failed due to a post-installation error.', mbError, MB_OK);
    end
    else
    begin
      MsgBox('The installation was successful. PyDD will now start automatically.', mbInformation, MB_OK);
      Exec('C:\ProgramData\IBCI\PyDD\win_start\vb_script\window_arrangement.exe', '', '', SW_SHOW, ewNoWait, tmpResultCode)
    end;
  end;
end;
